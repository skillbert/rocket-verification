
%toggle comment for 1/2/3 boosters (4 different placing in code)
sort BoosterId = struct main;
%sort BoosterId = struct left|right;
%sort BoosterId = struct left|right|extra;


act
	loop;

	%booster module
	ignite_bm:BoosterId;
	thrust_bm:BoosterId;
	detach_from_lm:BoosterId;
	
	s_startup_bm,r_startup_bm,c_startup_bm:BoosterId;
	s_stop_bm,r_stop_bm,c_stop_bm:BoosterId;
	s_confirm_bm_thrust,r_confirm_bm_thrust,c_confirm_bm_thrust:BoosterId;
	s_stopped_bm,r_stopped_bm,c_stopped_bm:BoosterId;
	s_fail_bm_thrust,r_fail_bm_thrust,c_fail_bm_thrust:BoosterId;

	%launch module
	ignite_lm;
	confirm_lm_thrust;
	fail_lm_thrust;
	stop_lm;
	thrust_lm;
	detach_ground_clamps;
	s_detach_bm,r_detach_bm,c_detach_bm:BoosterId;

	s_lm_abort,r_lm_abort,c_lm_abort;
	s_lm_ready_detach,r_lm_ready_detach,c_lm_ready_detach;

	%orbital module
	detach_lm;
	ignite_om;
	confirm_om_thrust;
	stop_om;
	fail_om_thrust;
	thrust_om;

	s_om_abort,r_om_abort,c_om_abort;
	s_om_ready_detach,r_om_ready_detach,c_om_ready_detach;
	s_detact_ascent_cover,r_detact_ascent_cover,c_detact_ascent_cover;

	%crew module
	detach_om;
	abort_cm;
	abort_pad;
	chutes_cm;
	detach_heatshield_cm;
	airbags_cm;
	land_cm;
	do_space_stuff;
	detach_ascent_cover;

	%fuel sensors
	s_bm_fuel_empty,r_bm_fuel_empty,c_bm_fuel_empty;

	%altitude sensor/gps
	s_alt_stage_lm,r_alt_stage_lm,c_alt_stage_lm;
	s_alt_stage_om,r_alt_stage_om,c_alt_stage_om;
	
		
proc

	%predefined multiactions
	%toggle comment for 1/2/3 boosters
	s_stop_bm_all = s_stop_bm(main);
	s_start_bm_all = s_startup_bm(main);
	% s_stop_bm_all = s_stop_bm(left)|s_stop_bm(right);
	% s_start_bm_all = s_startup_bm(left)|s_startup_bm(right);
	% s_stop_bm_all = s_stop_bm(left)|s_stop_bm(right)|s_stop_bm(extra);
	% s_start_bm_all = s_startup_bm(left)|s_startup_bm(right)|s_startup_bm(extra);

	%@desc: Sends fuel empty signal when either of the boosters is about to run out of fuel
	%Fuelsensor = s_bm_fuel_empty;

	%@desc: Sends staging signals when orbit is heigh enough for different staging points
	Altimeter = s_alt_stage_lm . s_alt_stage_om;

	%@param(broken): The booster had a failure and can not be started again
	%@desc: This process defines the main actions for a BM (start, stop, detach).
	BM(side:BoosterId,broken:Bool) =
		(!broken) -> r_startup_bm(side) . ignite_bm(side) . BM_start(side)
		+ r_stop_bm(side) . BM(side,broken)
		+ r_detach_bm(side) . detach_from_lm(side) . Idle;
	
	%@desc: This process defines the possible actions while the BM is starting (fail, stop, on).
	BM_start(side:BoosterId) =
		s_confirm_bm_thrust(side) . BM_on(side)
		+ BM_fail(side);
	
	%@desc: This process defines the actions when the BM needs to stop which means it is broken.
	BM_stop(side:BoosterId) = s_stopped_bm(side) . BM(side,false);

	%@desc: This process defines the actions when a BM fails. 
	BM_fail(side:BoosterId) = (s_fail_bm_thrust(side) + r_stop_bm(side) . s_stopped_bm(side)) . BM(side,true);

	%@desc: When the BM is on it can thrust the engine, fail or it can be stopped. .
	BM_on(side:BoosterId) = 
		thrust_bm(side) . BM_on(side)
		+ BM_fail(side)
		+ s_bm_fuel_empty . r_stop_bm(side) . BM_stop(side);
 

	%@desc: The LM and BM startup happen in parallel. Ignite LM, BMLeft and BMRight, 
	LM_startup = (ignite_lm . s_start_bm_all) . LM_ground(false,{});
	
	%@param(engine_on): Wether the main LM engine is on
	%@param(bm_engine): Set of BM engines that are confirmed to be on
	%@desc: This process checks if the LM, BMLeft and BMRight thrusters and engines or on. If this is the case the LM can launch and 
	%		proceed to the LM_fly process or if something goes wrong, the LM_ground_abort process.
	LM_ground(engine_on:Bool,bm_engine:Set(BoosterId)) =
		sum side:BoosterId. !(side in bm_engine) -> r_confirm_bm_thrust(side) . LM_ground(engine_on,bm_engine+{side})
		+ (!engine_on) -> confirm_lm_thrust . LM_ground(true,bm_engine)
		+ (engine_on && forall side:BoosterId. side in bm_engine) -> detach_ground_clamps . LM_fly
		+ (engine_on) -> thrust_lm . LM_ground(engine_on,bm_engine)
		+ ((sum side:BoosterId. r_fail_bm_thrust(side)) + fail_lm_thrust) . LM_ground_abort;

	%@desc: This process defines the actions the LM performs when it is in the air (fly, abort, stop).
	LM_fly =
		thrust_lm . LM_fly 		
		+ r_bm_fuel_empty . s_stop_bm_all . LM_stop_bm({side:BoosterId|true})
		+ ((sum side:BoosterId. r_fail_bm_thrust(side)) + fail_lm_thrust) . LM_abort;

	%@param(bm_on): Set of booster engines that are on
	%@desc: This process describes the actions the LM performs when it needs to stop the BMs. It gets both the BM's state in a parallel action(on or off), 
	%		If one of the booster modules failed or stopped we turn off the BM.
	LM_stop_bm(bm_on:Set(BoosterId)) = 
		(exists side:BoosterId. side in bm_on) -> (
			sum side:BoosterId. (side in bm_on) -> (r_stopped_bm(side) + r_fail_bm_thrust(side)) . LM_stop_bm(bm_on-{side})
			+ thrust_lm . LM_stop_bm(bm_on)
			+ fail_lm_thrust . LM_abort
		) <> LM_sep_bm({side:BoosterId|true});

	%@param(bm_attached): Set of boosters that are attached to LM
	%@desc: This process describes the actions the LM performs when separating from the BMs. If it is separated and out of
	%		the atmosphere it will start the LM_fly_space process.	
	LM_sep_bm(bm_attached:Set(BoosterId)) = 
		(exists side:BoosterId. side in bm_attached) -> (
			sum side:BoosterId. (side in bm_attached) -> s_detach_bm(side) . LM_sep_bm(bm_attached-{side})
			+ thrust_lm . LM_sep_bm(bm_attached)
			+ fail_lm_thrust . LM_abort
			+ LM_abort %add sep fail action
		) <> LM_fly_space;

	%@desc: This process defines the actions when the LM needs to abort. It stops both BMs, stops the LM and proceeds
	%		to the LM_safe process.
	LM_abort = s_lm_abort . s_stop_bm_all . stop_lm . Idle;

	%@desc: This process defines the actions when the LM is in space. The LM can fail resulting in an abort or it can succesfully
	%		achieve orbit in which case it stops and ends in a non-active process.
	LM_fly_space = 
		thrust_lm . LM_fly_space
		+ fail_lm_thrust . s_lm_abort
		+ r_alt_stage_lm . stop_lm . s_lm_ready_detach . Idle;
	

	%@desc: launch aborted at pad, rocket still on ground
	LM_ground_abort = s_stop_bm_all . stop_lm . abort_pad .  Idle;

	Idle = loop . Idle;
	%TODO remove
	LM_ignore_bm = 
		sum side:BoosterId. r_confirm_bm_thrust(side) . LM_ignore_bm
		+sum side:BoosterId. r_fail_bm_thrust(side) . LM_ignore_bm;

	OM_wait = r_lm_ready_detach . OM;
	OM = detach_lm . s_detact_ascent_cover . ignite_om . (
		confirm_om_thrust . OM_fly
		+ fail_om_thrust . stop_om . s_om_abort . Idle
	);
	
	%@desc The OM is in orbit, When it is ready to detach it sends the message to the CM and terminates
	OM_fly = 
		thrust_om . OM_fly
		+ fail_om_thrust . stop_om . s_om_abort . Idle
		+ r_alt_stage_om . stop_om . s_om_ready_detach . Idle;

	%crew and service module
	%@desc The crew can get abort signals or a ready signal from lower stages
	%@param(hascover) indicates if the launch cover is still attached
	CM(hascover:Bool) = 
		(hascover) -> r_detact_ascent_cover . detach_ascent_cover . CM(false)
		+ (r_lm_abort + r_om_abort) . detach_om . abort_cm . ((hascover) -> detach_ascent_cover <> tau) . CM_land
		+ (!hascover) -> r_om_ready_detach . detach_om . do_space_stuff . CM_land;


	%TODO add  chutes, heatshield, airbags
	CM_land = chutes_cm . detach_heatshield_cm . airbags_cm . land_cm . Idle;
	

init allow(
	{

		loop,

		%booster module
		ignite_bm,
		thrust_bm,
		detach_from_lm,

		
		%toggle comment for 1/2/3 boosters
		c_startup_bm,
		c_stop_bm,
		% c_startup_bm|c_startup_bm,
		% c_stop_bm|c_stop_bm,
		% c_startup_bm|c_startup_bm|c_startup_bm,
		% c_stop_bm|c_stop_bm|c_stop_bm,
		
		c_confirm_bm_thrust,
		c_stopped_bm,
		c_fail_bm_thrust,

		%launch module
		ignite_lm,
		confirm_lm_thrust,
		fail_lm_thrust,
		stop_lm,
		thrust_lm,
		detach_ground_clamps,
		c_detach_bm,

		c_lm_abort,
		c_lm_ready_detach,

		%orbital module
		detach_lm,
		ignite_om,
		confirm_om_thrust,
		stop_om,
		fail_om_thrust,
		thrust_om,

		c_om_abort,
		c_om_ready_detach,
		c_detact_ascent_cover,

		%crew module
		detach_om,
		abort_cm,
		abort_pad,
		land_cm,
		do_space_stuff,
		detach_ascent_cover,
		chutes_cm,
		detach_heatshield_cm,
		airbags_cm,

		%fuel sensors
		c_bm_fuel_empty,

		%altitude sensor/gps
		c_alt_stage_lm,
		c_alt_stage_om
	},
	comm({
		s_startup_bm|r_startup_bm->c_startup_bm,
		s_stop_bm|r_stop_bm->c_stop_bm,
		s_confirm_bm_thrust|r_confirm_bm_thrust->c_confirm_bm_thrust,
		s_stopped_bm|r_stopped_bm->c_stopped_bm,
		s_fail_bm_thrust|r_fail_bm_thrust->c_fail_bm_thrust,
		s_detach_bm|r_detach_bm->c_detach_bm,
		s_lm_abort|r_lm_abort->c_lm_abort,
		s_lm_ready_detach|r_lm_ready_detach->c_lm_ready_detach,
		s_om_abort|r_om_abort->c_om_abort,
		s_om_ready_detach|r_om_ready_detach->c_om_ready_detach,
		s_detact_ascent_cover|r_detact_ascent_cover->c_detact_ascent_cover,
		s_bm_fuel_empty|r_bm_fuel_empty->c_bm_fuel_empty,
		s_alt_stage_lm|r_alt_stage_lm->c_alt_stage_lm,
		s_alt_stage_om|r_alt_stage_om->c_alt_stage_om
	},
		%toggle comment for 1/2/3 boosters
		BM(main,false)
		% BM(left,false) || BM(right,false)
		% BM(left,false) || BM(right,false) || BM(extra,false)
		 || LM_startup || OM_wait || CM(true) || Altimeter
	)
);
